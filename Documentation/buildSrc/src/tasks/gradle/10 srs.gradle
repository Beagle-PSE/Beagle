/**
 * Tasks to build the Software Requirements Specification
 *
 * @author Joshua Gleitze
 */
 
def srssrc = file("$projectDir/src/srs/lyx")
 
task srsUml {
	description 'Renders the UML Diagrams used by the requirements specification.'
	
	// import the UmletRenderer as DSL extension	
	extensions.create('umletrender', UmletRenderer, project)
	
	ext {
		src = srssrc
		dest = file("$buildDir/tmp/umletrender")
	}

	def inputUxfs = fileTree(src).include('**/*.uxf')
	def outputUxfs = fileTree(dest).include('**/*.uxf')
	
	inputs.files inputUxfs
	outputs.upToDateWhen { 
		(inputUxfs.size() == outputUxfs.size()) && (outputUxfs.find { !file("${it}.pdf").exists() } == null)
	}
	
	doLast {
		dest.mkdirs()
		
		copy {
			from src
			into dest
		}
		
		umletrender {
			render fileTree(dest).include('**/*.uxf')
		}
	}
}
 
task srs(type: LyXRenderTask) {
	dependsOn srsUml
	
	group 'Documentation'
	description 'Renders the Requirements Specification.'
	
	from srsUml.dest
	document "Requirements Specification.lyx"
	into buildDir
	
	glossary true
	bibliography true
	
	// override the dir for up to date check because this is the dir that decides.
	inputs.dir srssrc
}

build.dependsOn srs
