/**
 * Creates Java classes out of the the PCM Source Statement Link XSL definition.
 *
 * @author Joshua Gleitze
 */
 
buildscript {
	repositories {
		jcenter()
		mavenCentral()
	}

	dependencies {
		classpath group: 'com.github.jacobono', name: 'gradle-jaxb-plugin', version: '1.3.6+'
	}
}

import org.gradle.jacobo.plugins.JaxbPlugin
apply plugin: JaxbPlugin

dependencies {
	jaxb group: 'com.sun.xml.bind', name: 'jaxb-xjc', version: '2.2.7-b41'
	jaxb group: 'com.sun.xml.bind', name: 'jaxb-impl', version: '2.2.7-b41'
	jaxb group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.7'
}

def psslSchemaDefinitionFolder = "Core/src/main/resources/de/uka/ipd/sdq/beagle/core/pcmsourcestatementlink"
def genFolderName = 'gen/main/java'
def genFolder = file(genFolderName)
def psslInputFiles = [ "$rootDir/$psslSchemaDefinitionFolder/PCM Source Statement Links.xsd", "$rootDir/$psslSchemaDefinitionFolder/PCM Source Statement Links Bindings.xjb" ]

def userLocale = Locale.getDefault()

jaxb {
	xsdDir = psslSchemaDefinitionFolder
	bindingsDir = psslSchemaDefinitionFolder
	bindings = ['PCM Source Statement Links Bindings.xjb']
	
	xjc {     
		destinationDir = genFolderName
		producesDir = genFolder
		episodesDir = 'Core/build/tmp/jaxb-episodes'
		header = false
		args = ['-mark-generated', '-npa']
	}
}

task prepareXjc {
	inputs.files psslInputFiles
	outputs.dir genFolder

	doLast {
		System.setProperty('javax.xml.accessExternalSchema', 'all')
		Locale.setDefault(Locale.UK)
	
		delete genFolder
		genFolder.mkdirs()
	}
}

task followUpXjc {
	inputs.files psslInputFiles
	outputs.dir genFolder
	
	doLast {
		Locale.setDefault(userLocale)
	}
}

task cleanGenerated(type: Delete) {
	delete 'gen'
}

clean.dependsOn cleanGenerated

xjc.inputs.files psslInputFiles
xjc.outputs.dir genFolder
xjc.finalizedBy followUpXjc
xjc.dependsOn prepareXjc

compileJava.dependsOn xjc
compileJava.source genFolder
