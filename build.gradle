/**
 * Collections of subprojects. Useful for configure blocks: configure(prototypeSubprojects){}
 */
/**
 * Prototype subprojects. Detected by being in the 'Prototypes' subfolder
 */
ext.prototypeSubprojects = subprojects.findAll { project ->
	project.name.startsWith('Prototype')
}
/**
 * Java subprojects. Detected by having the 'src/main/java' folder.
 */
ext.javaSubprojects = subprojects.findAll { project ->
	file("$project.projectDir/src/main/java").exists()
}
/**
 * Eclipse Plugin subprojects. Detected by having 'org.eclipse.pde.PluginNature' in their
 * .project file.
 */
ext.eclipseSubprojects = subprojects.findAll { project ->
	file("$project.projectDir/.project").with { 
		exists() && text.contains('org.eclipse.pde.PluginNature')
	}
}

allprojects {
	// All subprojects have a build and clean task
	if (tasks.findByPath('build') == null) {
		task build {
			group 'Build'
			description 'Assembles and tests this project.'
		}
	}
	
	if (tasks.findByPath('clean') == null) {
		task clean {
			group 'Build'
			description 'Deletes the build directory.'
			
			doLast {
				file(buildDir).deleteDir()
			}
		}
	}
	
	buildscript {
		fileTree("$projectDir/buildSrc/src/tasks/gradle/buildscript-dependencies").include('*.gradle').each { file ->
		 	project.apply from: file, to: buildscript
		} 
	}

	// import all tasks defined in src/build/groovy
	fileTree("$projectDir/buildSrc/src/tasks/gradle").include('*.gradle').sort().each { file ->
		project.apply from: file
	} 
}

defaultTasks 'build'
