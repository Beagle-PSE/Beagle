/**
 * Tasks for creating and deploying Beagle’s web presence.
 *
 * @author Joshua Gleitze
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '2.1+'
    }
}


import de.undercouch.gradle.tasks.download.Download

final String WEB_DEST = "$buildDir/web"

task downloadWebFonts(type: Download) {
	description 'Downloads fonts required by Beagle’s web presence'
	
    src ([
    	'http://checkmyworking.com/cm-web-fonts/Computer%20Modern%20Serif.zip',
    	'http://checkmyworking.com/cm-web-fonts/Computer%20Modern%20Sans.zip',
    	'http://fontawesome.io/assets/font-awesome-4.5.0.zip'
    ])
    dest "$buildDir/tmp/fonts/"
	onlyIfNewer true
	quiet true
	
	outputs.upToDateWhen { 
		fileTree(downloadWebFonts.dest).include('*.zip').size() == src.size()
	}
	
	doFirst {
		dest.mkdirs()
	}
}

task copyWebFonts(dependsOn: downloadWebFonts) {
	description 'Copies downloaded fonts for Beagle’s web presence into the web directory.'
	
	def cmsansInputDirname = 'Sans'
	def cmsansOutputDirname = 'cm-sans'
	def cmserifInputDirname = 'Serif'
	def cmserifOutputDirname = 'cm-serif'
	def faInputDirname = 'font-awesome-4.5.0'
	def faOutputDirname = 'font-awesome'
	
	
	ext {
		dest = "$WEB_DEST/fonts"
		cmsansdest = "$dest/$cmsansOutputDirname"
		cmserifdest = "$dest/$cmserifOutputDirname"
		fontawesomedest = "$dest/$faOutputDirname"
	}
	
    inputs.file downloadWebFonts.dest
	outputs.file dest
	
	doLast {	
		def sourcePaths = []
		def allZips = fileTree(downloadWebFonts.dest).include('*.zip')
		allZips.each { file -> sourcePaths.add zipTree(file) }
		
		copy {
	    	from sourcePaths
	    	into dest
	    }
	    
	    file("$dest/$cmsansInputDirname").renameTo(file(cmsansdest))
	    file("$dest/$cmserifInputDirname").renameTo(file(cmserifdest))
	    file("$dest/$faInputDirname").renameTo(file(fontawesomedest))
	}
}

task renderWebPages(type: Copy) {
	description 'Renders the pages of Beagle’s web presence.'
    inputs.dir "$projectDir/src/web/handlebars"
    
    def compileAction = new CompileWebFilesAction()
    compileAction.addRootTemplateFiles	'html': file("$projectDir/src/web/handlebars/roottemplate.html"),
    									'htmd': file("$projectDir/src/web/handlebars/roottemplate.html")
    HandlebarsRenderer.partialsFolder = file("$projectDir/src/web/handlebars/partials")
    HandlebarsRenderer.partialsExtension = "part"
    BeagleWebContext.project = project
    
	from "$projectDir/src/web/handlebars/pages"
	into WEB_DEST
	eachFile compileAction
}

task copyWebAssets(type: Copy, dependsOn: copyWebFonts) {
	description 'Copies web assets (scripts, stylesheets, …) into Beagle’s web presence.'
	
	ext {
		cssdest = "$WEB_DEST/css"
	}
	
	from "$projectDir/src/web"
	include "css/**/*.css"
	into WEB_DEST
}

task copyWebArtefacts() {
	description 'Copies all build artefacts that will be published into Beagle’s web presence.'
	
	mustRunAfter javadoc, srs, design, check, jacocoTestReport
	
	// file names of the copied files. Change the rename while copying.
	def checkstyleFileName = 'checkstyle.html'
	def srsFileName = 'Requirements Specification.pdf'
	def designFileName = 'Design and Architecture.pdf'
	
	ext {
		javadocDest = "$WEB_DEST/javadoc"
		srsDest = "$WEB_DEST/$srsFileName"
		checkstyleDest = "$WEB_DEST/$checkstyleFileName"
		jacocoDest = "$WEB_DEST/coverage"
		designDest = "$WEB_DEST/$designFileName"
	}
	
	inputs.files javadoc.destinationDir, srs.dest, design.dest, checkstyleHtml.dest, jacocoTestReport.reports.html.destination
	outputs.files javadocDest, srsDest, designDest, checkstyleDest, jacocoDest
	
	doLast {
		copy {	
			from javadoc.destinationDir
			into javadocDest
		}	
		
		copy {
			from srs.dest
			rename {file -> srsFileName}
			into WEB_DEST
		}
		
		copy {
			from design.dest
			rename {file -> designFileName}
			into WEB_DEST
		}
		
		copy {
			from checkstyleHtml.dest
			rename {file -> checkstyleFileName}
			into WEB_DEST
		}
		
		copy {
			from jacocoTestReport.reports.html.destination
			into jacocoDest
		}
	}
}

task web(dependsOn: [renderWebPages, copyWebAssets, copyWebArtefacts]) {
    group "Build"
    description "Builds Beagle’s web presence."
    
	ext {
		dest = WEB_DEST
	}
}
