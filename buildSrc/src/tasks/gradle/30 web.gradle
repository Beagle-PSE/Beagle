/**
 * Tasks for creating and deploying Beagle’s web presence.
 *
 * @author Joshua Gleitze
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'de.undercouch', name: 'gradle-download-task', version: '2.1+'
    }
}


import de.undercouch.gradle.tasks.download.Download

final String WEB_DEST = "$buildDir/web"

task downloadWebFonts(type: Download) {
    src ([
    	'http://checkmyworking.com/cm-web-fonts/Computer%20Modern%20Serif.zip',
    	'http://checkmyworking.com/cm-web-fonts/Computer%20Modern%20Sans.zip',
    	'http://fontawesome.io/assets/font-awesome-4.5.0.zip'
    ])
    dest "$buildDir/tmp/fonts/"
	onlyIfNewer true
	quiet true
	
	outputs.upToDateWhen { 
		fileTree(downloadWebFonts.dest).include('*.zip').size() == src.size()
	}
	
	doFirst {
		dest.mkdirs()
	}
}

task copyWebFonts(dependsOn: downloadWebFonts) {
	ext {
		dest = "$WEB_DEST/fonts"
	}
	
    inputs.file downloadWebFonts.dest
	outputs.file dest
	
	doLast {	
		def sourcePaths = []
		def allZips = fileTree(downloadWebFonts.dest).include('*.zip')
		allZips.each { file -> sourcePaths.add zipTree(file) }
		
		copy {
	    	from sourcePaths
	    	into dest
	    }
	}
}

task renderWebPages(type: Copy) {
    inputs.dir "$projectDir/src/web/handlebars"
    
    def compileAction = new CompileWebFilesAction()
    compileAction.addRootTemplateFiles	'html': file("$projectDir/src/web/handlebars/roottemplate.html"),
    									'htmd': file("$projectDir/src/web/handlebars/roottemplate.html")
    HandlebarsRenderer.partialsFolder = file("$projectDir/src/web/handlebars/partials")
    HandlebarsRenderer.partialsExtension = "part"
    BeagleWebContext.project = project
    
	from "$projectDir/src/web/handlebars/pages"
	into WEB_DEST
	eachFile compileAction
}

task copyWebAssets(type: Copy, dependsOn: copyWebFonts) {
	from "$projectDir/src/web"
	include "css/**/*.css"
	into WEB_DEST
}

task copyWebArtefacts() {
	mustRunAfter javadoc, srs, check, jacocoTestReport
	def checkstyleFileName = 'checkstyle.html'
	def srsFileName = 'Requirements Specification.pdf'
	
	ext {
		javadocDest = "$WEB_DEST/javadoc"
		srsDest = "$WEB_DEST/$srsFileName"
		checkstyleDest = "$WEB_DEST/$checkstyleFileName"
		jacocoDest = "$WEB_DEST/coverage"
	}
	
	inputs.files javadoc.destinationDir, srs.pdfdest, checkstyleHtml.dest, jacocoTestReport.reports.html.destination
	outputs.files javadocDest, srsDest, checkstyleDest, jacocoDest
	
	doLast {
		copy {	
			from javadoc.destinationDir
			into javadocDest
		}	
		
		copy {
			from srs.pdfdest
			rename {file -> srsFileName}
			into WEB_DEST
		}
		
		copy {
			from checkstyleHtml.dest
			rename {file -> checkstyleFileName}
			into WEB_DEST
		}
		
		copy {
			from jacocoTestReport.reports.html.destination
			into jacocoDest
		}
	}
}

task web(dependsOn: [renderWebPages, copyWebAssets, copyWebArtefacts]) {
    group "Build"
    description "Builds Beagle’s web presence."
    
	ext {
		dest = WEB_DEST
	}
}
