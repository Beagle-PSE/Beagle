/**
 * Deploys projects that are used to test Beagle.
 *
 * @author Joshua Gleitze
 */
 
buildscript {
	repositories {
		jcenter()
	}
	
	dependencies {
    	classpath group: 'at.bxm.gradleplugins', name: 'gradle-svntools-plugin', version: 'latest.release'
	}
}

import at.bxm.gradleplugins.svntools.tasks.SvnCheckout
import at.bxm.gradleplugins.svntools.SvnToolsPlugin

apply plugin: SvnToolsPlugin

def tmp = file("$buildDir/tmp/test-resources")

def specJvmSource = 'https://svnserver.informatik.kit.edu/i43/svn/code/BySuite/ByCounter/trunk/SPECjvm2008_1.00/'
def fileShareSource = 'https://svnserver.informatik.kit.edu/i43/svn/code/CaseStudies/FileShare/trunk/'

def fileShareDest = file("$projectDir/src/test/resources/PalladioFileShare")
def specJvmDest = file("$projectDir/src/test/resources/SPECjvm2008")

task dowloadFileShare(type: SvnCheckout) {
	group "Build Setup"
	description "Deploys PalladioFileShare to be used for testing Beagle"
	outputs.dir fileShareDest
	
	svnUrl fileShareSource
	workspaceDir fileShareDest
	username = 'anonymous'
	password = 'anonymous'
	
	doFirst {
		delete fileShareDest
	}
	
	doLast {
		delete "$projectDir/.subversion"
	}
}


task cleanTestProjects {
	description "Removes all test projects downloaded."
	group "Build Setup"

	doLast {
		delete fileShareDest
	}	
}

configure(javaSubprojects) {
	test.dependsOn rootProject.tasks.dowloadFileShare
	clean.dependsOn rootProject.tasks.cleanTestProjects
}
	