/**
 * Tasks to build the Software Requirements Specification
 *
 * @author Joshua Gleitze
 */
 
import static groovyx.gpars.GParsPool.withPool

def srssrc = file("$projectDir/doc/Requirements Specification")
 
task srsUml {
	description 'Renders the UML Diagrams used by the requirements specification.'
	
	// import the UmletRenderer as DSL extension	
	extensions.create('umletrender', UmletRenderer, project)
	
	ext {
		src = srssrc
		dest = file("$buildDir/tmp/umletrender")
	}

	def inputUxfs = fileTree(src).include('**/*.uxf')
	def outputUxfs = fileTree(dest).include('**/*.uxf')
	
	inputs.files inputUxfs
	outputs.upToDateWhen { 
		(inputUxfs.size() == outputUxfs.size()) && (outputUxfs.find { !file("${it}.pdf").exists() } == null)
	}
	
	doLast {
		dest.mkdirs()
		
		copy {
			from src
			into dest
		}
		// executed in parallel by a thread pool with 8 threads
 		withPool(8) {
			fileTree(dest).include('**/*.uxf').eachParallel { file ->
				umletrender {
					render file
				}
			}
		}
	}
}
 
task srs(type: LyXRenderTask) {
	dependsOn srsUml
	
	group 'Documentation'
	description 'Renders the Requirements Specification.'
	
	from srsUml.dest
	document "Requirements Specification.lyx"
	
	glossary true
	bibliography true
	
	// override the dir for up to date check because this is the dir that decides.
	inputs.dir srssrc
}

build.dependsOn srs
