/**
 * Defininitions configuring gradleâ€™s integration into Eclipse.
 *
 * @author Joshua Gleitze
 */
 
// All dependencies will be packed into this jar
def dependencyJarDest = file("$buildDir/dependencies.jar")

configure(javaSubprojects) {
	apply plugin: 'eclipse'
	
	configurations {
		production
		tests
	}
	
	configurations.compile.extendsFrom configurations.production
	configurations.testCompile.extendsFrom configurations.tests

	// we only want to export the test dependencies to Eclipse.
	eclipse {
		classpath {
			plusConfigurations = [project.configurations.tests, project.configurations.production]
			
			downloadSources true
			downloadJavadoc true
		}
	}
}

task dependencyJar(type: Jar) {
	def allDependencies = javaSubprojects.collect { project ->
		(project.configurations.production + project.configurations.tests).files
	}.flatten()
	
	description "Packs all dependencies into one jar"
	inputs.files allDependencies
	outputs.file dependencyJarDest
		
	destinationDir = dependencyJarDest.parentFile
	archiveName = dependencyJarDest.name
	
	from allDependencies.collect { zipTree(it) }
}

build.dependsOn dependencyJar
